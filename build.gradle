group 'org.nww'
version '1.0.0'

ext {
	artifactId = 'NetzwerkWohnen'
	nwwNexusUrl = 'http://81.169.212.240:8081/nexus/content/repositories/NetzwerkWohnen/'
}

buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'spring-boot' 
apply plugin: 'maven'
apply plugin: 'jacoco'

jar {
	baseName = artifactId
	version = project.version
}

repositories {
	maven {
		url nwwNexusUrl
	}
	mavenCentral()
}

configurations {
    providedRuntime
    deployerJars
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.RELEASE"
	}
}

dependencies {
	compile('org.springframework.data:spring-data-mongodb')
	compile('org.springframework.data:spring-data-elasticsearch')
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile('org.springframework.boot:spring-boot-starter-thymeleaf') {
    	exclude group: "org.javassist", module: "javassist"
    }
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4")
    compile("io.github.jpenren:thymeleaf-spring-data-dialect:2.1.1")
    compile("org.springframework.boot:spring-boot-starter-web")    
    compile("org.springframework.boot:spring-boot-starter-actuator")
	compile("org.springframework.boot:spring-boot-starter-websocket")
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-feign')
    
    compile("org.jsoup:jsoup:1.8.3")
    /*
    compile("org.webjars:webjars-locator")
    compile("org.webjars:sockjs-client:1.0.2")
    compile("org.webjars:stomp-websocket:2.3.3")
    */
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    
    testCompile('junit:junit')
    testCompile('org.assertj:assertj-core:3.5.2')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.mockito:mockito-all:1.8.4')
	
	deployerJars("org.apache.maven.wagon:wagon-http:2.7")
}


eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

test { 
	finalizedBy jacocoTestReport 
}

jacoco {
    toolVersion = "0.7.4+"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

uploadArchives {
	dependsOn assemble
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars

			repository(url: nwwNexusUrl) {
				authentication(
						userName: nexusUser,
						password: nexusPassword
				)
			}

			pom {
				groupId = project.group
				artifactId = artifactId
				version = project.version
			}
		}
	}
}